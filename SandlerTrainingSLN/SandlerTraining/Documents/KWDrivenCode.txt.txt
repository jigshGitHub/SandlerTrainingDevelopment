'Developed by www.LearnQTP.com
Option Explicit

Dim iTestCases ' Total Number of Test Cases
Dim iTestSteps ' Total Number of Test Steps
Dim sTestCaseID ' Stores the current test case ID
Dim sStepResult ' Stores the result of test step
Dim i, j ' Loop counters

'Add The two Test Sheets at run time 
DataTable.AddSheet "TCases"
DataTable.AddSheet "TSteps"

'Import the two sheets in the corresponding sheets added above
DataTable.ImportSheet "C:\Program Files\HP\QuickTest Professional\Tests\KW_Framework\KeyWordDriveFramework.xls", "TCases", "TCases"
DataTable.ImportSheet "C:\Program Files\HP\QuickTest Professional\Tests\KW_Framework\KeyWordDriveFramework.xls", "TSteps", "TSteps"

'Count The Total Number Of rows (or test cases) in the TCases sheet
iTestCases = Datatable.GetSheet("TCases").GetRowCount

For i = 1 to iTestCases
	Datatable.GetSheet("TCases").SetCurrentRow(i)
	
	If Datatable.Value("Execute","TCases") = "Yes" Then

	sTestCaseID = Datatable.GetSheet("TCases").GetParameter("TestCaseID") 'Get the Test Case ID that needs to be executed
	iTestSteps = Datatable.GetSheet("TSteps").GetRowCount 'Get the total row count of steps sheets

			For j= 1 To iTestSteps
				Datatable.GetSheet("TSteps").SetCurrentRow(j)
				If Datatable.GetSheet("TSteps").GetParameter("TestCaseID") = sTestCaseID Then
			

				Select Case Datatable.Value("Keyword", "TSteps")
				Case "StartApp"
					sStepResult = StartApp()

				Case "Login"
					sStepResult = Login()

				Case "FlightFinder"
					sStepResult = FlightFinder()

				Case "SelectFlight"
					sStepResult = SelectFlight()

				Case "Cruise"
					sStepResult = Cruise()

				Case "SignOff"
					sStepResult = SignOff()

				Case "CloseBrowser"
					sStepResult = CloseBrowser()

                End Select
				Datatable.Value("Result", "TSteps") = sStepResult			

				End If
			Next

			
			If  sStepResult = "Pass" Then
				Datatable.Value("Result", "TCases") = "Pass"
   			End If

	End If

Next

DataTable.ExportSheet "C:\Program Files\HP\QuickTest Professional\Tests\KW_Framework\KeyWordDriveFramework-Results.xls", "TCases"
DataTable.ExportSheet "C:\Program Files\HP\QuickTest Professional\Tests\KW_Framework\KeyWordDriveFramework-Results.xls", "TSteps"




